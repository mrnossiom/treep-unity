base := "/tmp/treep"

_default:
	@just --list --unsorted --list-heading '' --list-prefix '—— '

# Start the game binary with developement features
run *ARGS:
	cargo run -F development {{ARGS}}

# Creates temporary folder and pipes to simplify log management
[linux]
setup-many:
	mkdir -p {{base}}
	mkfifo {{base}}/server {{base}}/client1 {{base}}/client2

# Start a server and two clients to test multiplayer
many *ARGS:
	@just run -- server {{ARGS}} 2>{{base}}/server &
	@just run -- client {{ARGS}} 2>{{base}}/client1 &
	@just run -- client {{ARGS}} 2>{{base}}/client2 &

# Runs the procedural generation debug tool
proc-gen *ARGS:
	# ensure output folder is present
	@mkdir -p target/out
	cargo run --bin proc_gen -- {{ARGS}}
